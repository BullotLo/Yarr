# Plotting tools
include(ExternalProject)
function(YARR_ADD_PLOTTING)
    ExternalProject_Add (
        plotting_tools
        GIT_REPOSITORY https://gitlab.cern.ch/YARR/utilities/plotting-tools.git
        GIT_TAG master
        CMAKE_ARGS "-DTARGET_INSTALL_AREA=${TARGET_INSTALL_AREA}"
        PREFIX "${CMAKE_CURRENT_BINARY_DIR}/external"
	INSTALL_COMMAND make install
        )
endfunction()

# TBB
function(YARR_ADD_TBB)
    if (TARGET tbb_2020)
        return()
    endif()

    # Architecture
    if ("${TARGET_ARCH}" STREQUAL "arm")
        set(TBB_ARCH "arch=arm")
    else()
        if ("${TARGET_ARCH}" STREQUAL "arm64")
            set(TBB_ARCH "arch=arm64")
        else()
            set(TBB_ARCH "")
        endif()
    endif()
    if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
        set(TBB_COMP compiler=clang)
    else()
        set(TBB_COMP CXX=${CMAKE_CXX_COMPILER})
    endif()

    ExternalProject_Add (
        tbb_2020
        GIT_REPOSITORY https://github.com/intel/tbb.git
        GIT_TAG 2020_U2
        UPDATE_COMMAND ""
        INSTALL_COMMAND ${PROJECT_SOURCE_DIR}/scripts/install_tbb.sh ${CMAKE_CURRENT_BINARY_DIR}  ${CMAKE_CURRENT_BINARY_DIR}
        BUILD_COMMAND cd ${CMAKE_CURRENT_BINARY_DIR}/external/src/tbb_2020 &&  make -j4 ${TBB_COMP} ${TBB_ARCH} extra_inc=big_iron.inc
        CONFIGURE_COMMAND ""
        PREFIX "${CMAKE_CURRENT_BINARY_DIR}/external"
        )
    link_directories(${CMAKE_CURRENT_BINARY_DIR}/lib)
    include_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/tbb_2020/include)
endfunction()

function(YARR_ADD_NETIO)
    if (TARGET netio4)
        return()
    endif()

    # NetIO
    ExternalProject_Add (
        netio4
        GIT_REPOSITORY https://:@gitlab.cern.ch:8443/YARR/netio.git
        GIT_TAG felix-4.0.x
        UPDATE_COMMAND ""
        PATCH_COMMAND cp ${PROJECT_SOURCE_DIR}/cmake/CMakeLists.txt.netio ${CMAKE_CURRENT_BINARY_DIR}/external/src/netio4/CMakeLists.txt
        INSTALL_COMMAND ""
        PREFIX "${CMAKE_CURRENT_BINARY_DIR}/external"
        CMAKE_ARGS "${TOOLCHAIN}" -DCMAKE_CXX_FLAGS=-fPIC
        DEPENDS tbb_2020
        )

    include_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/netio4)
    link_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/netio4-build)
endfunction()

function(YARR_ADD_FELIX)
    if (TARGET felixbase4)
        return()
    endif()

    # FELIX
    ExternalProject_Add (
        felixbase4
        GIT_REPOSITORY https://:@gitlab.cern.ch:8443/YARR/felixbase.git
        GIT_TAG felix-4.0.x
        UPDATE_COMMAND ""
        PATCH_COMMAND ""
        INSTALL_COMMAND ""
        CONFIGURE_COMMAND ""
        BUILD_COMMAND ""
        PREFIX "${CMAKE_CURRENT_BINARY_DIR}/external"
        DEPENDS netio4
        )

    include_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/felixbase4)
endfunction()

function(YARR_ADD_ROGUE)
    if (TARGET rogue)
        return()
    endif()

    # Rogue
    ExternalProject_Add (
        rogue
        GIT_REPOSITORY https://github.com/slaclab/rogue.git
        GIT_TAG v3.6.0
        UPDATE_COMMAND ""
        INSTALL_COMMAND make install
        PREFIX "${CMAKE_CURRENT_BINARY_DIR}/external"
        CMAKE_ARGS "-DNO_PYTHON=1" "-DNO_ZMQ=1" "-DNO_BZIP=1" "-DSTATIC_LIB=1" "${TOOLCHAIN}"  "-DCMAKE_POSITION_INDEPENDENT_CODE=ON"
        DEPENDS tbb_2020
        )

    link_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/rogue/lib)
    include_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/rogue/include)
endfunction()

function(YARR_ADD_VARIANT)
    if (TARGET variant)
        return()
    endif()

    # Variant
    ExternalProject_Add (
        variant
        GIT_REPOSITORY  https://gitlab.cern.ch/atlas-sw-misc/variant.git
        GIT_TAG v1.5.5
        UPDATE_COMMAND ""
        INSTALL_COMMAND ""
        BUILD_COMMAND ""
        CONFIGURE_COMMAND ""	
        PREFIX "${CMAKE_CURRENT_BINARY_DIR}/external"
        )
    link_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/variant)
    include_directories(${CMAKE_CURRENT_BINARY_DIR}/external/src/variant/include)
endfunction()

# Plotting Tools
if (NOT DISABLE_PLOTTING_TOOLS)
set(CMAKE_MESSAGE_LOG_LEVEL "ERROR")
find_package(ROOT COMPONENTS Hist Graf)
set(CMAKE_MESSAGE_LOG_LEVEL "STATUS")
if (${ROOT_FOUND})
    YARR_ADD_PLOTTING()
    MESSAGE(STATUS "Compiling plotting tools!")
    MESSAGE(STATUS "ROOT requires CXXFLAGS: ${ROOT_CXX_FLAGS}" )
else (${ROOT_FOUND})
    MESSAGE(STATUS "Could not locate ROOT. Will not compile plotting tools!")
endif (${ROOT_FOUND})
endif ()
